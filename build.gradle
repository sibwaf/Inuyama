buildscript {
    ext.kotlinVersion = "1.4.10"
    ext.coroutinesVersion = "1.3.9"

    ext.objectboxVersion = "2.3.4"

    ext.kodeinVersion = "6.3.3"

    ext.okHttpVersion = "4.2.1"
    ext.jsoupVersion = "1.13.1"
    ext.gsonVersion = "2.8.6"

    ext.date4jVersion = "1.5.1"

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.4.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "io.objectbox:objectbox-gradle-plugin:$objectboxVersion"
    }
}

subprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://dl.bintray.com/kodein-framework/Kodein-DI/" }
    }

    afterEvaluate {
        def androidPlugin = getPlugins().findPlugin("com.android.application")
        if (androidPlugin == null) {
            androidPlugin = getPlugins().findPlugin("com.android.library")
        }

        if (androidPlugin != null) {
            (androidPlugin as com.android.build.gradle.BasePlugin).extension.with {
                sourceSets {
                    main.java.srcDirs += "src/main/kotlin"
                    test.java.srcDirs += "src/test/kotlin"
                }
            }
        }
    }
}
