apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"

def localProperties = null
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.newInputStream().withCloseable {
        def properties = new Properties()
        properties.load(it)
        localProperties = properties
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "ru.dyatel.inuyama"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 3
        versionName "1.2"
    }

    if (localProperties != null) {
        signingConfigs {
            release {
                storeFile file(localProperties.getProperty("signing.store.file"))
                storePassword localProperties.getProperty("signing.store.password")
                keyAlias localProperties.getProperty("signing.key.name")
                keyPassword localProperties.getProperty("signing.key.password")
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    implementation "org.kodein.di:kodein-di-generic-jvm:$kodeinVersion"
    implementation "org.kodein.di:kodein-di-framework-android-x:$kodeinVersion"

    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"

    implementation "com.google.android.material:material:1.1.0-alpha02"

    implementation "androidx.work:work-runtime-ktx:2.2.0"

    def ankoVersion = "0.10.8"
    implementation "org.jetbrains.anko:anko-commons:$ankoVersion"
    implementation "org.jetbrains.anko:anko-sdk21:$ankoVersion"
    implementation "org.jetbrains.anko:anko-appcompat-v7:$ankoVersion"
    implementation "org.jetbrains.anko:anko-recyclerview-v7:$ankoVersion"
    implementation "org.jetbrains.anko:anko-cardview-v7:$ankoVersion"
    implementation "org.jetbrains.anko:anko-design:$ankoVersion"

    implementation "io.objectbox:objectbox-kotlin:$objectboxVersion"
    releaseImplementation "io.objectbox:objectbox-android:$objectboxVersion"
    debugImplementation "io.objectbox:objectbox-android-objectbrowser:$objectboxVersion"

    def magellanVersion = "1.1.0"
    implementation "com.wealthfront:magellan:$magellanVersion"
    implementation "com.wealthfront:magellan-support:$magellanVersion"

    def fastAdapterVersion = "3.3.1"
    implementation "com.mikepenz:fastadapter:$fastAdapterVersion"
    implementation "com.mikepenz:fastadapter-commons:$fastAdapterVersion"

    implementation "com.mikepenz:materialize:1.2.1"
    implementation "com.mikepenz:materialdrawer:6.1.2"
    implementation "com.mikepenz:iconics-core:3.1.0"
    implementation "com.mikepenz:community-material-typeface:2.7.94.1@aar"

    implementation "com.github.bumptech.glide:glide:4.9.0"

    implementation "org.jsoup:jsoup:$jsoupVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"

    implementation "org.ocpsoft.prettytime:prettytime:4.0.1.Final"
    implementation "com.darwinsys:hirondelle-date4j:$date4jVersion"

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"

    implementation project(":Common")
}

apply plugin: "io.objectbox"